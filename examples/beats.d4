::scale delay ticks env lowpass;

96bpm
4* keep tempo

8 tempo@ ticks swap
from

short_kick,
tempo@1\+ short_kick,
hat,
drop 0,

dup clap swap short_kick+,
kick 2/,
dup hat swap tempo@1\+ kick 2/+,
drop 0,

choose.

8 tempo@ 3/ ticks swap
from G low, B', G, D, D', C, E, G choose
4/ *sq 10/
0.3s 0.8 delay
0.5*.


(instruments: age -- signal)

:kick
    dup
    60Hz over * swap           (-- age carrier age)
    4Hz * sin 6*               (-- age carrier modulator)
    +sin                       (-- age signal)
    swap 0.01s 0.3s env
;

:short_kick
    dup
    60Hz over * swap           (-- age carrier age)
    4Hz * sin 6*               (-- age carrier modulator)
    +sin                       (-- age signal)
    over 0.01s 0.2s env swap

    dup
    120Hz over * swap           (-- age carrier age)
    20Hz * sin 6*               (-- age carrier modulator)
    +sin                       (-- age signal)
    swap 0 0.05s env
    +
;

:clap
    noise
    swap 0s 0.15s env
    0.3 lowpass
;

:hat
    noise 8/
    swap 0s 0.05s env
;
